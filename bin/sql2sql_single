#!/usr/bin/env perl
################################################################################
# WeBWorK Online Homework Delivery System
# Copyright © 2000-2006 The WeBWorK Project, http://openwebwork.sf.net/
# $CVSHeader: webwork2/bin/sql2sql_single,v 1.2 2004/10/22 22:59:46 sh002i Exp $
# 
# This program is free software; you can redistribute it and/or modify it under
# the terms of either: (a) the GNU General Public License as published by the
# Free Software Foundation; either version 2, or (at your option) any later
# version, or (b) the "Artistic License" which comes with this package.
# 
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See either the GNU General Public License or the
# Artistic License for more details.
################################################################################

=head1 NAME

sql2sql_single - convert a course from the C<sql> to the C<sql_single> database
layout.

=head1 SYNOPSIS

 sql2sql_single COURSEID NEWDBNAME DBADMIN DBPASS

=head1 DESCRIPTION

Convert a course database using the C<sql> database layout to use the
<sql_single> database layout instead. This is done by moving the tables of the
course database into the single database used by the C<sql_single> database
layout. For this to work, both layouts must be using the same database server.

NEWDBNAME gives the name of the database used by C<sql_single>. This is usually
"webwork". DBADMIN and DBPASS are respectively the username and password of an
administrative account on the SQL server.

After running this script, you must edit the course's F<course.conf> file,
chainging the database layout from C<sql> to C<sql_single>.

=cut

BEGIN {
	# hide arguments (there could be passwords there!)
	$0 = "$0";
}

use strict;
use warnings;
use DBI;
use Data::Dumper;

BEGIN {
	die "WEBWORK_ROOT not found in environment.\n"
		unless exists $ENV{WEBWORK_ROOT};
}

use lib "$ENV{WEBWORK_ROOT}/lib";
use WeBWorK::CourseEnvironment;
use WeBWorK::DB;
use WeBWorK::Utils::CourseManagement qw/dbLayoutSQLSources/;

sub usage {
	print STDERR "usage: $0 COURSEID NEWDBNAME ADMINUSER ADMINPASSWORD\n";
	exit;
}

sub usage_error {
	print STDERR "$0: @_\n";
	usage();
}

# get command-line options
my ($courseID, $newDBName, $dbUsername, $dbPassword) = @ARGV;

# perform sanity check
usage_error("must specify COURSEID.") unless $courseID and $courseID ne "";
usage_error("must specify NEWDBNAME.") unless defined $newDBName and $newDBName ne "";
usage_error("must specify DBADMIN.") unless defined $dbUsername;
usage_error("must specify DBPASS.") unless defined $dbPassword;

# bring up a minimal course environment
my $ce = WeBWorK::CourseEnvironment->new({
	webwork_dir => $ENV{WEBWORK_ROOT},
	courseName => $courseID,
});

# make sure the course actually uses the 'sql' layout
usage_error("$courseID: does not use 'sql' database layout.") unless $ce->{dbLayoutName} eq "sql";

# get database layout source data
my %sql_sources = dbLayoutSQLSources($ce->{dbLayouts}{sql});

foreach my $source (keys %sql_sources) {
	my %source = %{$sql_sources{$source}};
	my @tables = @{$source{tables}};
	
	my $dbh = DBI->connect($source, $dbUsername, $dbPassword);
	
	my $stmt = "RENAME TABLE";
	
	foreach my $table (@tables) {
		my $curr_table_name = $ce->{dbLayouts}{sql}{$table}{params}{tableOverride} || $table;
		my $new_table_name = $ce->{dbLayouts}{sql_single}{$table}{params}{tableOverride} || $table;
		$stmt .= " `$curr_table_name` TO `$newDBName`.`$new_table_name`,";
	}
	
	substr($stmt, -1, 1, ";");
	unless ($dbh->do($stmt)) {
		die "An error occured while trying to convert the course database.\n",
			"It is possible that the course database was partially transferred.\n",
			"The DBI error message was:\n\n",
			$dbh->errstr, "\n";
	}
	
	$dbh->disconnect;
}

# warn about modifying course.conf
my $course_dot_conf = $ce->{courseFiles}{environment};

print "\nThe course database for course '$courseID' has been moved to the 'sql_single' database '$newDBName'. To complete the transition, you must edit the course's course.conf file, located at: $course_dot_conf\n\n";

print <<'EOT';
In that file, there should be lines like:
	$dbLayoutName = 'sql';
	*dbLayout = $dbLayouts{$dbLayoutName};
You must change these likes to:
	$dbLayoutName = 'sql_single';
	*dbLayout = $dbLayouts{$dbLayoutName};
The following sed(1) command may be of use:
	sed 's/sql/sql_single/' < course.conf > course.conf.new

Until you do this, the course will not work.
EOT

=head1 AUTHOR

Written by Sam Hathaway, hathaway at users.sourceforge.net.

=cut
