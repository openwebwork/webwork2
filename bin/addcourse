#!/usr/bin/env perl
################################################################################
# WeBWorK Online Homework Delivery System
# Copyright © 2000-2003 The WeBWorK Project, http://openwebwork.sf.net/
# $CVSHeader: webwork-modperl/bin/addcourse,v 1.4 2003/12/27 21:41:56 sh002i Exp $
# 
# This program is free software; you can redistribute it and/or modify it under
# the terms of either: (a) the GNU General Public License as published by the
# Free Software Foundation; either version 2, or (at your option) any later
# version, or (b) the "Artistic License" which comes with this package.
# 
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See either the GNU General Public License or the
# Artistic License for more details.
################################################################################

=head1 NAME

addcourse - add a course

=head1 SYNOPSIS

 addcourse [options] COURSEID

=head1 DESCRIPTION

Add a course to the courses directory. The required directories will be
created. Optionally, a database can be populated with users and the
F<templates> directory can be populated with the contents of another directory.
Also, one or more users can be granted professor privileges.

=head1 OPTIONS

=over

=item B<--templates>=I<DIR>

The contents of the directory I<DIR> will be copied to the F<templates>
directory of the new course.

=item B<--db-layout>=I<LAYOUT>

The specified database layout will be used in place of the default specified in
F<global.conf>.

=item B<--users>=I<FILE>

The users listed in the comma-separated text file I<FILE> will be added to the
user list of the new course. The format of this file is the same as user lists
exported from WeBWorK.

=item B<--professors>=I<USERID>[,I<USERID>]...

Each I<USERID>, if it is present in the new course's user list, will be granted
professor privileges (i.e. a permission level of 10). Requires B<--users>.

=item I<COURSEID>

The name of the course to create.

=back

=cut

use strict;
use warnings;
use Getopt::Long;

BEGIN {
	die "WEBWORK_ROOT not found in environment.\n"
		unless exists $ENV{WEBWORK_ROOT};
}

use lib "$ENV{WEBWORK_ROOT}/lib";
use WeBWorK::CourseEnvironment;
use WeBWorK::DB;
use WeBWorK::Utils qw(readFile cryptPassword);

sub usage {
	print STDERR "$0 [options] COURSEID\n";
	print STDERR "  [--templates=DIR] [--db-layout=LAYOUT]\n";
	print STDERR "  [--users=FILE [--professors=USERID[,USERID]...] ]\n";
	exit;
}

my $templates = "";
my $dbLayout = "";
my $users = "";
my @professors = ();

GetOptions(
	"templates=s" => \$templates,
	"db-layout=s" => \$dbLayout,
	"users=s" => \$users,
	"professors=s" => \@professors,
);
my %professors = map { $_ => 1 } map { split /,/ } @professors;
my $courseID = shift;

unless ($courseID) {
	print STDERR "$0: must specify COURSEID.\n";
	usage();
};

if (@professors and not $users) {
	print STDERR "$0: can't specify --professors without also specifying --users.\n";
	usage();
}

# bring up a minimal course environment
my $ce = WeBWorK::CourseEnvironment->new($ENV{WEBWORK_ROOT}, "FAKE_URL_ROOT",
	"FAKE_PG_ROOT", $courseID);

if ($dbLayout) {
	die "Database layout $dbLayout does not exist in the course environment.",
			" (It must be defined in global.conf.)\n"
		unless exists $ce->{dbLayouts}->{$dbLayout};
}

# collect some data
my $coursesDir = $ce->{webworkDirs}->{courses};
my $courseDir = "$coursesDir/$courseID";

# make sure the course doesn't already exist
if (-e $courseDir) {
	die "$courseID: course exists\n";
}

# create required directories
my @subDirs = sort values %{ $ce->{courseDirs} };
foreach my $subDir (@subDirs) {
	print "mkdir $subDir\n";
	mkdir "$subDir"
		or die "Failed to create course directory $subDir: $!\n";
}

if ($templates) {
	unless (-d "$courseDir/templates") {
		warn "$courseDir/templates: not found, creating:\n";
		print "mkdir $courseDir/templates\n";
		mkdir "$courseDir/templates"
			or die "Failed to mkdir $courseDir/templates: $!\n";
	}
	print "copy $templates/* -> $courseDir/templates\n";
	system "/bin/cp -r $templates/* $courseDir/templates/"
		and die "Failed to copy $templates/* to $courseDir/templates: $!\n";
}

if ($users) {
	# import users - much of this code is burgled from UserList.pm
	
	my $db;
	if ($dbLayout) {
		# use the specified layout
		$db = WeBWorK::DB->new($ce->{dbLayouts}->{$dbLayout});
	} else {
		# use the default layout
		$db = WeBWorK::DB->new($ce->{dbLayout});
	}
	
	my @contents = split /\n/, readFile($users);
	
	foreach my $string (@contents) {
		$string =~ s/^\s+//;
		$string =~ s/\s+$//;
		my (
			$student_id, $last_name, $first_name, $status, $comment,
			$section, $recitation, $email_address, $user_id
		) = split /\s*,\s*/, $string;
		
		my $User = $db->newUser;
		$User->user_id($user_id);
		$User->first_name($first_name);
		$User->last_name($last_name);
		$User->email_address($email_address);
		$User->student_id($student_id);
		$User->status($status);
		$User->section($section);
		$User->recitation($recitation);
		$User->comment($comment);
		
		my $PermissionLevel = $db->newPermissionLevel;
		$PermissionLevel->user_id($user_id);
		if (exists $professors{$user_id}) {
			$PermissionLevel->permission(10);
		} else {
			$PermissionLevel->permission(0);
		}
		
		my $Password = $db->newPassword;
		$Password->user_id($user_id);
		$Password->password(cryptPassword($student_id));
		
		$db->addUser($User);
		$db->addPermissionLevel($PermissionLevel);
		$db->addPassword($Password);
		
		if (exists $professors{$user_id}) {
			print "add professor $user_id\n";
			delete $professors{$user_id};
		} else {
			print "add user $user_id\n";
		}
	}
	
	if (my @ids = keys %professors) {
		print STDERR "warning: @ids not in imported user list.\n";
	}
}

=head1 BUGS

Databases are created using the database layout specified in the global
configuration file (F<global.conf>), which requires users to temporarily modify
their system's configuration in order to create a course with a nonstandard
database layout.

Also, some database drivers are unable to create storage for their data. The
GDBM backend can do this, but the SQL backend cannot (currently).

=head1 AUTHOR

Written by Sam Hathaway, hathaway at users.sourceforge.net.

=cut
