#!/usr/bin/env perl
################################################################################
# WeBWorK Online Homework Delivery System
# Copyright &copy; 2000-2024 The WeBWorK Project, https://github.com/openwebwork
#
# This program is free software; you can redistribute it and/or modify it under
# the terms of either: (a) the GNU General Public License as published by the
# Free Software Foundation; either version 2, or (at your option) any later
# version, or (b) the "Artistic License" which comes with this package.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See either the GNU General Public License or the
# Artistic License for more details.
################################################################################

use strict;
use warnings;
use Getopt::Std;
use DBI;
use Data::Dumper;

my $pg_dir;
BEGIN {
	die "WEBWORK_ROOT not found in environment.\n" unless exists $ENV{WEBWORK_ROOT};
	$pg_dir = $ENV{PG_ROOT} // "$ENV{WEBWORK_ROOT}/../pg";
	die "The pg directory must be defined in PG_ROOT" unless (-e $pg_dir);
}
use lib "$ENV{WEBWORK_ROOT}/lib";
use lib "$pg_dir/lib";
use WeBWorK::CourseEnvironment;
use WeBWorK::Utils qw/runtime_use/;


our ($opt_v);
getopts("v");

if ($opt_v) {
	$| = 1;
	*verbose = sub { print STDERR @_ };
} else {
	*verbose = sub {};
}

# global variables, hah hah.
my ($dbh, %sql_tables);

################################################################################

my $i = -1;
our @DB_VERSIONS;

$DB_VERSIONS[++$i]{desc} = "is the initial version of database, identical to database structure in WeBWorK 2.2.x.";

$DB_VERSIONS[++$i]{desc} = "adds dbupgrade table to facilitate automatic database upgrades.";
$DB_VERSIONS[  $i]{global_code} = sub {
	$dbh->do("CREATE TABLE `dbupgrade` (`name` VARCHAR(255) NOT NULL PRIMARY KEY, `value` TEXT)");
	$dbh->do("INSERT INTO `dbupgrade` (`name`, `value`) VALUES (?, ?)", {}, "db_version", 1);
	$sql_tables{dbupgrade} = ();
};


$DB_VERSIONS[++$i]{desc} = "adds depths table to keep track of dvipng depth information.";
$DB_VERSIONS[  $i]{global_code} = sub {
	$dbh->do("CREATE TABLE depths (md5 CHAR(33) NOT NULL, depth SMALLINT, PRIMARY KEY (md5))");
	$sql_tables{depths} = ();
};

$DB_VERSIONS[++$i]{desc} = "adds locations, location_addresses, set_locations and set_locations_user tables to database, and add restrict_ip to set and set_user.";
$DB_VERSIONS[  $i]{global_code} = sub {
	$dbh->do("CREATE TABLE locations (location_id TINYBLOB NOT NULL, description TEXT, PRIMARY KEY (location_id(1000)))");
	$dbh->do("CREATE TABLE location_addresses (location_id TINYBLOB NOT NULL, ip_mask TINYBLOB NOT NULL, PRIMARY KEY (location_id(500),ip_mask(500)))");
};

our $THIS_DB_VERSION = $i;

################################################################################

my $ce = WeBWorK::CourseEnvironment->new({
	webwork_dir => $ENV{WEBWORK_ROOT},
});

$dbh = DBI->connect(
	$ce->{database_dsn},
	$ce->{database_username},
	$ce->{database_password},
	{
		PrintError => 0,
		RaiseError => 1,
	},
);

{
	verbose("Obtaining dbupgrade lock...\n");
	my ($lock_status) = $dbh->selectrow_array("SELECT GET_LOCK('dbupgrade', 10)");
	if (not defined $lock_status) {
		print "Couldn't obtain lock because an error occurred.\n";
		exit 2;
	}
	if ($lock_status) {
		verbose("Got lock.\n");
	} else {
		print "Timed out while waiting for lock.\n";
		exit 2;
	}
}

%sql_tables = get_sql_tables();

my $db_version = 0;


verbose("Initial db_version is $db_version\n");

if ($db_version > $THIS_DB_VERSION) {
	print "db_version is $db_version, but the current database version is only $THIS_DB_VERSION. This database was probably used with a newer version of WeBWorK.\n";
	exit;
}

while ($db_version < $THIS_DB_VERSION) {
	$db_version++;
	unless (upgrade_to_version($db_version)) {
		print "\nUpgrading from version ".($db_version-1)." to $db_version failed.\n\n";
		unless (ask_permission("Ignore this error and go on to the next version?", 0)) {
			exit 3;
		}
	}
	set_db_version($db_version);
}

print "\nDatabase is up-to-date at version $db_version.\n";

END {
	verbose("Releasing dbupgrade lock...\n");
	my ($lock_status) = $dbh->selectrow_array("SELECT RELEASE_LOCK('dbupgrade')");
	if (not defined $lock_status) {
		print "Couldn't release lock because the lock does not exist.\n";
		exit 2;
	}
	if ($lock_status) {
		verbose("Released lock.\n");
	} else {
		print "Couldn't release lock because the lock is not held by this thread.\n";
		exit 2;
	}
}

################################################################################

sub get_sql_tables {
	my $sql_tables_ref = $dbh->selectcol_arrayref("SHOW TABLES");
	my %sql_tables; @sql_tables{@$sql_tables_ref} = ();

	return %sql_tables;
}

sub set_db_version {
	my $vers = shift;
	$dbh->do("UPDATE `dbupgrade` SET `value`=? WHERE `name`='db_version'", {}, $vers);
}

sub upgrade_to_version {
	my $vers = shift;
	my %info = %{$DB_VERSIONS[$vers]};

	print "\nUpgrading database from version " . ($vers-1) . " to $vers...\n";
	my $desc = $info{desc} || "has no description.";
	print "(Version $vers $desc)\n";

	if (exists $info{global_code}) {
		eval { $info{global_code}->() };
		if ($@) {
			print "\nAn error occurred while running the system upgrade code for version $vers:\n";
			print "$@";
			return 0 unless ask_permission("Ignore this error and keep going?", 0);
		}
	}
	print "Done.\n";
	return 1;
}

################################################################################

sub ask_permission {
	my ($prompt, $default) = @_;

	$default = 1 if not defined $default;
	my $options = $default ? "[Y/n]" : "[y/N]";

	while (1) {
		print "$prompt $options ";
		my $resp = <STDIN>;
		chomp $resp;
		return $default if $resp eq "";
		return 1 if lc $resp eq "y";
		return 0 if lc $resp eq "n";
		$prompt = 'Please enter "y" or "n".';
	}
}
