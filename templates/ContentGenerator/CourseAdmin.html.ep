% use WeBWorK::Utils::CourseManagement qw(listCourses listArchivedCourses);
% use WeBWorK::Utils qw(getAssetURL);
%
% content_for js => begin
	<%= javascript getAssetURL($ce, 'js/apps/SelectAll/selectall.js'), defer => undef =%>
% end
%
% unless ($authz->hasPermissions(param('user'), 'create_and_delete_courses')) {
	<div class="alert alert-danger p-1 mb-0">
		<%= maketext('You are not authorized to create or delete courses.') %>
	</div>
	% last;
% }
%
<ul class="nav nav-pills justify-content-center my-2">
	% for (
		% [
		% 	'add_course',
		% 	maketext('Add Course'),
		% 	{
		% 		add_admin_users      => 1,
		% 		add_config_file      => 1,
		% 		add_dbLayout         => 'sql_single',
		% 		add_templates_course => $ce->{siteDefaults}{default_templates_course} || ''
		% 	}
		% ],
		% [ 'rename_course',        maketext('Rename Course') ],
		% [ 'delete_course',        maketext('Delete Course') ],
		% [ 'archive_course',       maketext('Archive Course') ],
		% [ 'unarchive_course',     maketext('Unarchive Course') ],
		% [ 'upgrade_course',       maketext('Upgrade Courses') ],
		% [ 'manage_locations',     maketext('Manage Locations') ],
		% [ 'hide_inactive_course', maketext('Hide Courses') ],
	% )
	% {
		<li class="nav-item">
			<%= link_to $_->[1] => $cg->systemLink($urlpath,
					params => { subDisplay => $_->[0], %{ $_->[2] // {} } }
				),
				class => 'nav-link' . ((param('subDisplay') // '') eq $_->[0] ? ' active' : '') =%>
		</li>
	% }
</ul>
%
<hr class="mt-0">
%
<%= include 'ContentGenerator/CourseAdmin/registration_form', cg => $cg; %>
%
% if (@{ $cg->{errors} }) {
	<div class="alert alert-danger p-1 mb-2">
		<div class="mb-1"><%= maketext('Please correct the following errors and try again:') %></div>
		<ul class="mb-1">
			% for (@{ $cg->{errors} }) {
				<li><%= $_ %></li>
			% }
		</ul>
	</div>
% }
%
% if ($cg->{method_to_call}) {
	% my $method_to_call = $cg->{method_to_call};
	<%= $cg->$method_to_call =%>
% } else {
	% unless (-w $ce->{webworkDirs}{logs} && -w $ce->{webworkDirs}{tmp} && -w $ce->{webworkDirs}{DATA}) {
		<h2><%= maketext('Directory permission errors') %></h2>
		<p>
			<%= maketext(
				'The webwork server must be able to write to these directories. Please correct the permssion errors.'
			) =%>
		</p>
		<ul>
			% unless (-w $ce->{webworkDirs}{logs}) {
				<li><%= maketext('unable to write to directory [_1]', $ce->{webworkDirs}{logs}) %></li>
			% }
			% unless (-w $ce->{webworkDirs}{tmp}) {
				<li><%= maketext('unable to write to directory [_1]', $ce->{webworkDirs}{tmp}) %></li>
			% }
			% unless (-w $ce->{webworkDirs}{DATA}) {
				<li><%= maketext('unable to write to directory [_1]', $ce->{webworkDirs}{DATA}) %></li>
			% }
		</ul>
	% }
	%
	<h2><%= maketext('Courses') %></h2>
	% my @courseIDs = listCourses($ce);
	<ol>
		% for (sort { lc($a) cmp lc($b) } listCourses($ce)) {
			% next if $_ eq 'admin' || $_ eq 'modelCourse';
			<li>
				<%= link_to $_ => $cg->systemLink(
					$urlpath->newFromModule('WeBWorK::ContentGenerator::ProblemSets', $c, courseID => $_),
					authen => 0
				) =%>
			</li>
		% }
	</ol>
	%
	<h2><%= maketext('Archived Courses') =%></h2>
	<ol>
		% for (sort { lc($a) cmp lc($b) } listArchivedCourses($ce)) {
			<li><%= $_ %></li>
		% }
	</ol>
% }
