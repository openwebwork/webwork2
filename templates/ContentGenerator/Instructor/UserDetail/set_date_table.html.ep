% my $setID = $globalRecord->set_id;
%
% # Modify set id to include the version if this is a versioned set.
% my $isGateway   = $globalRecord->assignment_type =~ /gateway/;
% my $isVersioned = $isGateway && defined $mergedRecord && $mergedRecord->can('version_id');
% $setID .= ',v' . $mergedRecord->version_id if $isVersioned;
%
<table>
	<tr>
		% unless ($isVersioned) {
			<th scope="col" colspan="2">
				<%= maketext('Assignment Dates') =%>
			</th>
		% }
		% if (defined $userRecord) {
			<th scope="col" <%== defined $userRecord && !$isVersioned ? '' : 'colspan="2"' %>>
				% if ($isVersioned) {
					<a tabindex="0" class="help-popup" role="button" data-bs-toggle="popover" data-bs-trigger="focus"
						data-bs-content="<%= maketext(
							'If the test was timed, granting the user an additional version '
							. 'may be preferred to changing its dates.' )%>">
						<i class="icon fa-solid fa-question-circle fa-lg" aria-hidden="true"></i><% =%>\
						<span class="visually-hidden"><%= maketext('Help Icon') %></span><% =%>\
					</a>
				% }
				<%= $isVersioned ? maketext(q{User's Test Version Dates}) : maketext('User Overrides') =%>
			</th>
		% }
	</tr>
	% for my $field (@$fields) {
		% # Skip reduced credit dates for sets which don't have them.
		% next
			% if $field eq 'reduced_scoring_date'
			% && (!$ce->{pg}{ansEvalDefaults}{enableReducedScoring} || !$globalRecord->enable_reduced_scoring);
		%
		<tr>
			<td class="px-1 text-nowrap">
				<%= label_for "set.$setID.$field" . (defined $userRecord ? '_id' : '.class_value') =>
						maketext($fieldLabels->{$field}),
					class => 'form-label mb-0' =%>
			</td>
			% unless ($isVersioned) {
				<td class="px-1 text-nowrap">
					<%= text_field "set.$setID.$field.class_value" =>
							$c->formatDateTime($globalRecord->$field, 'datetime_format_short'),
						id => "set.$setID.$field.class_value", readonly => undef, dir => 'ltr',
						class => 'form-control-plaintext form-control-sm w-auto',
						size => 16,
						defined $userRecord ? ('aria-labelledby' => "set.$setID.${field}_id") : (),
						data => { class_value => $globalRecord->$field } =%>
				</td>
			% }
			% if (defined $userRecord) {
				<td class="px-1 text-nowrap">
					<div class="input-group input-group-sm flex-nowrap flatpickr">
						<%= text_field "set.$setID.$field" => $userRecord->$field,
							id          => "set.$setID.${field}_id",
							class       => 'form-control w-auto' . ($field eq 'open_date' ? ' datepicker-group' : ''),
							placeholder => $isGateway
								? ($isVersioned && $field ne 'reduced_scoring_date'
									? maketext('Required')
								   	: maketext('Test Default'))
								: maketext('Assignment Default'),
							$isVersioned && $field ne 'reduced_scoring_date' ? (required => undef) : (),
							data => {
								input      => undef,
								done_text  => maketext('Done'),
								today_text => maketext('Today'),
								now_text   => maketext('Now'),
								locale     => $ce->{language},
								timezone   => $ce->{siteDefaults}{timezone}
							} =%>
						<a class="btn btn-secondary btn-sm" data-toggle tabindex="0" role="button"
							aria-label="<%= $c->maketext('Pick date and time') %>">
							<i class="fas fa-calendar-alt"></i>
						</a>
					</div>
				</td>
			% }
		</tr>
	% }
</table>
